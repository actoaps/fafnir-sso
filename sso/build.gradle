plugins {
	id 'java'
	id 'application'
	id 'com.github.bjornvester.wsdl2java' version '1.2'
	id 'de.undercouch.download' version '5.2.0'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
}

sourceCompatibility = 17
targetCompatibility = 17
compileJava.options.encoding = 'UTF-8'

def myVersion = System.properties['versionOverride']
if ((myVersion?.trim())){
	version = myVersion
} else {
	version = "1.0-SNAPSHOT"
}

bootJar.enabled = false
jar.enabled = true

applicationDefaultJvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:6001"]

test {
    ignoreFailures = false
	useJUnitPlatform()
}

repositories {
	mavenCentral()
}

mainClassName = 'dk.acto.fafnir.sso.Sso'

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.24'

	implementation project(':ui')
	implementation project(':fafnir-client')

    implementation 'io.vavr:vavr:0.10.4'
    implementation 'com.google.guava:guava:31.1-jre'

	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'commons-codec:commons-codec:1.15'

	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-mustache"
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-saml2-service-provider'

    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.0'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
	implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'com.hazelcast:hazelcast:5.1.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
	testImplementation project(':fafnir-client')

	annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'io.vavr:vavr:0.10.4'

	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
    implementation 'org.glassfish:jakarta.el:5.0.0-M1'

	implementation 'com.github.scribejava:scribejava-apis:8.3.1'
	implementation 'com.github.scribejava:scribejava-httpclient-okhttp:8.3.1'

	implementation 'com.auth0:java-jwt:4.0.0'

	//WSDL / Apache-cxf stuff
	runtimeOnly 'org.apache.cxf:cxf-rt-frontend-jaxws:3.5.3'
	runtimeOnly 'org.apache.cxf:cxf-rt-transports-http:3.5.3'
}

task downloadWSDLWsaGruppe(type: Download) {
	src 'https://wsagruppe.uni-login.dk/wsagruppe-v3/ws?WSDL'
	dest 'src/main/resources/wsdl/wsagruppe_v3.wsdl'
}
task downloadWSDLWsiBruger(type: Download) {
	src 'https://wsibruger.unilogin.dk/wsibruger-v6/ws?WSDL'
	dest 'src/main/resources/wsdl/wsibruger_v6.wsdl'
}
task downloadWSDLWsiInst(type: Download) {
	src 'https://wsiinst.unilogin.dk/wsiinst-v5/ws?WSDL'
	dest 'src/main/resources/wsdl/wsiinst_v5.wsdl'
}

wsdl2java {
	wsdlDir = layout.projectDirectory.dir("src/main/resources/wsdl")
	cxfVersion = "3.5.1"
	includesWithOptions = [
			"**/wsagruppe_v3.wsdl" : ["-wsdlLocation", "https://wsagruppe.uni-login.dk/wsagruppe-v3/ws?WSDL"],
			"**/wsibruger_v6.wsdl" : ["-wsdlLocation", "https://wsibruger.unilogin.dk/wsibruger-v6/ws?WSDL"],
			"**/wsiinst_v5.wsdl" : ["-wsdlLocation", "https://wsiinst.unilogin.dk/wsiinst-v5/ws?WSDL"]
	]
	options = ["-encoding", "UTF-8"]
}

tasks.wsdl2java.dependsOn tasks.downloadWSDLWsaGruppe, tasks.downloadWSDLWsiBruger, tasks.downloadWSDLWsiInst

distributions {
	main {
		distTar.archiveFileName = "sso.tar"
		distZip.enabled = false
		bootDistTar.enabled = false
		bootDistZip.enabled = false
	}
}
