plugins {
    id 'java'
    id 'org.sonarqube' version '3.4.0.2513' apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group = "dk.acto"
def myVersion = System.properties['versionOverride']
if ((myVersion?.trim())){
    version = myVersion
} else {
    version = "1.0-SNAPSHOT"
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "FAILED",
                    "PASSED",
                    "SKIPPED"
            "STANDARD_OUT"
            exceptionFormat "FULL"
            showExceptions true
            showCauses true
            showStackTraces true
        }
    }

    apply plugin: 'jacoco'

    plugins.withId('java') {
        jacoco {
            toolVersion = "0.8.8"
        }

        jacocoTestReport {
            dependsOn(test)
            reports {
                xml.enabled true
                csv.enabled false
                html.destination file("${buildDir}/jacocoHtml")
            }
        }

        test.finalizedBy(jacocoTestReport)
    }
}

if (System.properties['sonar.login'] != null) {
    apply plugin: 'org.sonarqube'

    subprojects {
        plugins.withId('java') {
            sonarqube {
                properties {
                    property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco"
                }
            }
        }
    }

    project.subprojects.each {build.dependsOn(":${it.name}:build")}

    task sonarAnalysis(group:"Sonarqube") {
        Task sonarTask = project.getTasks().getByName('sonarqube')
        doLast {
            sonarTask.run()
        }
    }
    build.finalizedBy(sonarAnalysis)
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

configure(subprojects) { project ->
    initializeSonatypeStagingRepository {
        shouldRunAfter(tasks.withType(Sign))
    }
}
