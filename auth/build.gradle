plugins {
	id 'java'
	id 'application'
	id 'net.ltgt.apt' version '0.10'
	id 'com.bmuschko.clover' version '2.2.1'
	id 'no.nils.wsdl2java' version '0.10'
	id "de.undercouch.download" version "3.4.3"
}

test {
	useTestNG()
}

repositories {
	jcenter()
	mavenCentral()
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

compileJava.options.encoding = 'UTF-8'

mainClassName = 'dk.acto.auth.Main'


dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.0'
	implementation 'org.hibernate.validator:hibernate-validator:6.0.11.Final'
    implementation 'org.glassfish:javax.el:3.0.1-b08'
	implementation 'io.vavr:vavr:0.9.2'
	implementation 'com.google.guava:guava:25.1-jre'

	testImplementation 'org.testng:testng:6.10'
	implementation 'com.sparkjava:spark-core:2.7.2'
	implementation 'com.github.scribejava:scribejava-apis:5.5.0'
	implementation 'com.github.scribejava:scribejava-httpclient-okhttp:5.5.0'
	implementation 'org.thymeleaf:thymeleaf:3.0.9.RELEASE'
	implementation 'com.sparkjava:spark-template-thymeleaf:2.7.1'

	implementation 'org.apache.httpcomponents:httpclient:4.5.6'

	implementation 'com.google.code.gson:gson:2.8.5'

	implementation "com.fasterxml.jackson.core:jackson-databind:2.9.5"
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.5'
	implementation 'org.apache.logging.log4j:log4j-core:2.11.0'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'

	implementation 'com.auth0:java-jwt:3.4.0'

	//WSDL/ Apache-cxf stuff
	implementation 'javax.xml.ws:jaxws-api:2.3.0'
	implementation 'javax.activation:activation:1.1.1'
	implementation 'javax.xml.bind:jaxb-api:2.3.0'

	implementation 'org.apache.cxf:cxf-rt-frontend-jaxws:3.2.5'
	implementation 'org.apache.cxf:cxf-rt-transports-http:3.2.5'
	implementation 'org.apache.cxf:cxf-rt-transports-http-jetty:3.2.5'

	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.0.1'
	implementation 'javax.jws:jsr181-api:1.0-MR1'

	apt "org.projectlombok:lombok:1.18.0"
	clover 'org.openclover:clover:4.2.1'

	wsdl2java 'javax.xml.ws:jaxws-api:2.3.0'
	wsdl2java 'javax.activation:activation:1.1.1'
	wsdl2java 'javax.xml.bind:jaxb-api:2.3.0'

	wsdl2java 'javax.annotation:javax.annotation-api:1.3.2'
	wsdl2java 'org.glassfish.jaxb:jaxb-runtime:2.3.0.1'
	wsdl2java 'javax.jws:jsr181-api:1.0-MR1'

}

task downloadWSDLWsaGruppe(type: Download) {
	src 'https://wsagruppe.uni-login.dk/wsagruppe-v1/ws?WSDL'
	dest 'src/main/resources/wsdl/wsagruppe_v1.wsdl'
}
task downloadWSDLWsiBruger(type: Download) {
	src 'https://wsibruger.uni-login.dk/wsibruger-v3/ws?WSDL'
	dest 'src/main/resources/wsdl/wsibruger_v3.wsdl'
}
task downloadWSDLWsiInst(type: Download) {
	src 'https://wsiinst.uni-login.dk/wsiinst-v2/ws?WSDL'
	dest 'src/main/resources/wsdl/wsiinst_v2.wsdl'
}

wsdl2java {
	encoding = 'utf-8'
	wsdlsToGenerate = [
			['-wsdlLocation', 'classpath:wsdl/wsagruppe_v1.wsdl', project.file('src/main/resources/wsdl/wsagruppe_v1.wsdl')],
			['-wsdlLocation', 'classpath:wsdl/wsibruger_v3.wsdl', project.file('src/main/resources/wsdl/wsibruger_v3.wsdl')],
			['-wsdlLocation', 'classpath:wsdl/wsiinst_v2.wsdl', project.file('src/main/resources/wsdl/wsiinst_v2.wsdl')],
	]
	generatedWsdlDir = project.file('src/main/java/dk/acto/auth/generated')
}

wsdl2javaExt {
	deleteGeneratedSourcesOnClean = true
}

task copyDependencies(type: Copy) {
	from configurations.compileClasspath
	into 'build/dependencies'
}

build.dependsOn(copyDependencies)
//wsdl2java.dependsOn(downloadWSDL)
wsdl2java.dependsOn = [downloadWSDLWsaGruppe, downloadWSDLWsiBruger, downloadWSDLWsiInst]
