plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

sourceCompatibility = 17
targetCompatibility = 17
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    ignoreFailures = false
    useJUnitPlatform()
}

def myVersion = System.properties['versionOverride']
if ((myVersion?.trim())){
    version = myVersion
} else {
    version = "1.0-SNAPSHOT"
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    implementation "io.vavr:vavr:0.10.4"
    annotationProcessor "io.vavr:vavr:0.10.4"

    implementation("io.jsonwebtoken:jjwt:0.9.1")
    implementation('com.fasterxml.jackson.core:jackson-databind:2.13.3')

    implementation 'com.hazelcast:hazelcast:5.1.1'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    implementation 'org.springframework:spring-context:5.3.20'
    implementation 'org.springframework.security:spring-security-web:5.7.1'

    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
    testImplementation("org.assertj:assertj-core:3.23.1")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = 'dk.acto'
                artifactId = 'fafnir-client'
                name = "Fafnir SSO Client"
                description = "Client library for the Fafnir SSO solution, for use with Spring Boot"
                url = "https://github.com/actoaps/fafnir-sso"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = 'EliasJorgensen'
                        name = 'Elias JÃ¸rgensen'
                        email = 'ej@acto.dk'
                    }
                }
                scm {
                    url ='https://github.com/actoaps/fafnir-sso'
                    connection = 'scm:https://github.com/actoaps/fafnir-sso.git'
                    developerConnection = 'scm:git://github.com/actoaps/fafnir-sso.git'
                }
            }

            repositories {
                maven {
                    url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
            }
        }
    }
}

signing {
    required { !project.version.endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning") }
    if (project.findProperty("signingKey")) {
        useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
    } else {
        useGpgCmd()
    }
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
