plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

sourceCompatibility = 17
targetCompatibility = 17
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    ignoreFailures = false
    useJUnitPlatform()
}

def myVersion = System.properties['versionOverride']
if ((myVersion?.trim())){
    version = myVersion
} else {
    version = "1.0-SNAPSHOT"
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    implementation "io.vavr:vavr:0.10.4"
    annotationProcessor "io.vavr:vavr:0.10.4"

    compileOnly 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    implementation 'com.hazelcast:hazelcast:5.2.0'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    implementation 'org.springframework:spring-context:5.3.23'
    implementation 'org.springframework.security:spring-security-web:5.7.5'

    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.1')
    testImplementation("org.assertj:assertj-core:3.23.1")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = 'dk.acto'
                artifactId = 'fafnir-client'
                name = "Fafnir SSO Client"
                description = "Client library for the Fafnir SSO solution, for use with Spring Boot"
                url = "https://github.com/actoaps/fafnir-sso"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = 'EliasJorgensen'
                        name = 'Elias JÃ¸rgensen'
                        email = 'ej@acto.dk'
                    }
                }
                scm {
                    url ='https://github.com/actoaps/fafnir-sso'
                    connection = 'scm:https://github.com/actoaps/fafnir-sso.git'
                    developerConnection = 'scm:git://github.com/actoaps/fafnir-sso.git'
                }
            }

            repositories {
                maven {
                    url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
            }
        }
    }
}

signing {
    required { !project.version.endsWith("-SNAPSHOT")}
    useInMemoryPgpKeys(findProperty("signingKey"), "")
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
